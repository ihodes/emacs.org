#+TITLE: Literate Configuration

* Prelude

  This is the beginning of a "literate" emacs configuration. This is both a way
  to learn more about ~org-mode~ and to better organize my configuration.

  So far I am very impressed with how it feels to write code like this. Logical
  and explicitly-named code-folding, with first class comments and
  hyperlinks, seems disproportionately useful and powerful.

* Look & Feel

  First things first, let's make emacs pretty.

  #+name: look-and-feel
  #+BEGIN_SRC emacs-lisp
    (load-theme 'tango-dark)

    (blink-cursor-mode 0)
    (setq initial-scratch-message "")
    (setq inhibit-startup-message t)
    (setq inhibit-startup-echo-area-message "isaachodes")
    (menu-bar-mode -1)
    (tool-bar-mode -1)
    (scroll-bar-mode -1)

    (show-paren-mode 1)
    (set-face-attribute 'mode-line nil
                        :foreground "gray100" :background "gray10"
                        :inverse-video nil
                        :box '(:line-width 6 :color "gray20" :style nil))
    (set-face-attribute 'mode-line-inactive nil
                        :foreground "gray30" :background "gray10"
                        :inverse-video nil
                        :box '(:line-width 6 :color "gray10" :style nil))
  #+END_SRC
* Packages

  Configure and load ~cask.el~, initializing the packages it manages [[file:Cask][Cask]].

  #+name: packages
  #+BEGIN_SRC emacs-lisp
    (require 'cask "/usr/local/Cellar/cask/0.7.2/cask.el")
    (cask-initialize)
  #+END_SRC

  ([[https://github.com/cask/cask][Cask]] can be installed with ~brew install cask~, and the packages managed by
  the Cask file can be installed with ~cask install~.)

* Defaults

  Here we set some defaults which generally make emacs a more well-behaved
  editor.

  #+name: defaults
  #+BEGIN_SRC emacs-lisp
    (global-undo-tree-mode)
    (column-number-mode t)
    (ido-mode t)
    (setq ido-enable-flex-matching t)
    (setq kill-ring-max 500)
    (setq js-indent-level 2)
    (setq tramp-default-method "ssh")
    (setq auto-save-default nil)

    (windmove-default-keybindings)

    (require 'uniquify)
    (setq-default uniquify-buffer-name-style 'forward)

    (require 'saveplace)
    (setq-default save-place t
                  save-place-file (concat user-emacs-directory "places"))

    (setq-default indent-tabs-mode nil
                  fill-column 80
                  show-trailing-whitespace t
                  auto-save-file-name-transforms `((".*" ,temporary-file-directory t))
                  enable-recursive-minibuffers t
                  apropos-do-all t
                  backup-directory-alist `(("." . ,(concat user-emacs-directory
                                                   "backups"))))
  #+END_SRC
* Modes

  Here we activate modes and do a lot of stuff which should be split out into
  smaller code blocks.

  We load and configure all packages with the glorious [[https://github.com/jwiegley/use-package][use-package]] mode.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (require 'use-package)
  #+END_SRC

  And configure most of them...

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (use-package flycheck
      :init
      (progn
        (add-hook 'after-init-hook 'global-flycheck-mode)
        (flycheck-define-checker jsxhint-checker
          "A JSX syntax and style checker based on JSXHint."
          :command ("jsxhint" (config-file "--config=" jshint-configuration-path) source)
          :error-patterns ((error line-start (1+ nonl) ": line " line ", col " column ", " (message) line-end))
          :modes (js-mode))
        (setq auto-mode-alist (remove (rassoc 'html-mode auto-mode-alist)
                                      auto-mode-alist))))

    (use-package ace-jump-mode
      :bind ("M-SPC" . ace-jump-mode))

    (use-package paredit
      :init
      (dolist (x '(scheme emacs-lisp lisp clojure))
        (add-hook
         (intern (concat (symbol-name x) "-mode-hook")) 'paredit-mode)))

    (use-package web-mode
      :mode
      (("\\.phtml\\'" . web-mode)
       ("\\.jsx\\'" . web-mode)
       ("\\.tpl\\.php\\'" . web-mode)
       ("\\.jsp\\'" . web-mode)
       ("\\.as[cp]x\\'" . web-mode)
       ("\\.erb\\'" . web-mode)
       ("\\.ejs\\'" . web-mode)
       ("\\.mustache\\'" . web-mode)
       ("\\.djhtml\\'" . web-mode)
       ("\\.html?\\'" . web-mode))
      :init
      (setq web-mode-engines-alist '(("erb" . "\\.ejs\\'"))))

    (use-package smart-mode-line
      :init
      (progn
        (setq sml/no-confirm-load-theme t)
        (sml/setup)
        (sml/apply-theme 'dark)
        (add-to-list 'sml/replacer-regexp-list '("^~/workspace/" ":W:") t)
        (add-to-list 'sml/hidden-modes " Undo-Tree")
        (add-to-list 'sml/hidden-modes " MRev")
        (add-to-list 'sml/hidden-modes " Paredit")
        (add-to-list 'sml/hidden-modes " hl-s")
        (add-to-list 'sml/hidden-modes " Helm")
        (add-to-list 'sml/hidden-modes " company")
        (add-to-list 'sml/hidden-modes " yas")))

    (use-package company
      :init
      (progn
        (add-hook 'after-init-hook 'global-company-mode)
        (add-hook 'after-init-hook
                  (lambda () (add-to-list 'company-backends 'company-anaconda)))))

    (use-package ag
      :bind ("<f2>" . ag-project)
      :init
      (setq ag-highlight-search t))

    (use-package recentf
      :init
      (progn
        (recentf-mode t)
        (setq recentf-max-saved-items 50)))

    (use-package expand-region
      :bind (("s-+" . er/expand-region)
             ("s--" . er/contract-region)))

    (use-package visual-regexp
      :bind (("C-c q" . vr/query-replace)
             ("C-c r" . vr/replace)
             ("s-c" . vr/mc-mark)))

    (use-package browse-kill-ring
      :bind ("C-M-y" . browse-kill-ring))

    (use-package undo-tree-mode
      :bind (("C-x C-u" . undo-tree-undo)
             ("C-x C-r" . undo-tree-redo)))

    (use-package haskell-mode
      :init
      (add-hook 'haskell-mode-hook 'turn-on-haskell-indentation))

    (defun endless/load-gh-pulls-mode ()
      "Start `magit-gh-pulls-mode' only after a manual request."
      (interactive)
      (require 'magit-gh-pulls)
      (add-hook 'magit-mode-hook 'turn-on-magit-gh-pulls)
      (magit-gh-pulls-mode 1)
      (magit-gh-pulls-reload))

    (use-package magit
      :bind ("C-x g" . magit-status)
      :config (define-key magit-mode-map "#gg"
                 'endless/load-gh-pulls-mode))

    (use-package projectile
      :bind ("s-p" . projectile-commander)
      :init
      (progn
        (projectile-global-mode)
        (setq projectile-mode-line
              '(:eval (format " @:%s" (projectile-project-name))))))

    (use-package rainbow-delimiters
      :init
      (add-hook 'prog-mode-hook 'rainbow-delimiters-mode))

    (use-package yasnippet
      :init
      (progn
        (setq yas-snippet-dirs '("~/.emacs.d/snippets"))
        (yas-global-mode 1)))
  #+END_SRC

  Finally we quick'n'dirtily set some little text modes.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (defvar ihodes/text-modes
      '(("\\.avpr?\\'" . js-mode)
        ("\\.avdl?\\'" . c-mode)
        ("\\.yml\\'" . yaml-mode)
        ("\\.markdown\\'" . markdown-mode)
        ("\\.md\\'" . markdown-mode)))

    (dolist (mm ihodes/text-modes)
      (add-to-list 'auto-mode-alist mm))
  #+END_SRC

  (And some misc. additional code...)

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    ;; https://github.com/purcell/exec-path-from-shell
    (when (memq window-system '(mac ns))
      (exec-path-from-shell-initialize))

    (add-hook 'sql-interactive-mode-hook '(lambda () (toggle-truncate-lines t)))
  #+END_SRC

** Org

Very rudimentary customization of ~org-mode~.

Primarily we set our [[http://mobileorg.ncogni.to/][MobileOrg]] directory so that we can sync with the iPhone (or
Android!) app. We also enable support for a variety of languages.

#+name: org
#+BEGIN_SRC emacs-lisp
  (bind-key "C-c c" 'org-capture)
  (bind-key "C-c a" 'org-agenda)

  (setq org-directory "~/org/")
  (setq org-default-notes-file "~/org/notes.org")
  (setq org-mobile-directory "~/Dropbox/Apps/MobileOrg")

  (org-babel-do-load-languages
   (quote org-babel-load-languages)
   (quote ((emacs-lisp . t)
           (ditaa . t)
           (dot . t)
           (clojure . t)
           (js . t)
           (R . t)
           (python . t)
           (ruby . t)
           (sh . t)
           (ledger . t)
           (org . t)
           (latex . t))))
  (setq org-src-fontify-natively nil)

  (setq org-modules '(org-info
                      org-habit))

  (org-load-modules-maybe t)
#+END_SRC

*** Emacs Capture

#+name: org
#+BEGIN_SRC emacs-lisp

  (defvar ihodes/org-basic-task-template "* TODO %^{Task}
  SCHEDULED: %^t
  %?
  :PROPERTIES:

  :Effort: %^{effort|1:00|0:05|0:15|0:30|2:00|4:00}
  :END:" "Basic task data")

  (defvar ihodes/org-basic-schedule-template "* %^{Event}
  %^T
  %?
  :PROPERTIES:

  :Location: %^{Location}
  :END:" "Basic schedule data")

  (setq org-capture-templates
        `(("t" "Tasks" entry
           (file+headline "~/org/organize.org" "Tasks")
           ,ihodes/org-basic-task-template)
          ("s" "Schedule" entry
           (file+headline "~/org/organize.org" "Schedule")
           ,ihodes/org-basic-schedule-template)
          ("j" "Journal entry" plain
           (file+datetree+prompt "~/org/journal.org")
           "%?\n")
          ("w" "Work journal entry" plain
           (file+datetree+prompt "~/org/work-journal.org")
           "%?\n")
          ("m" "Meeting" plain
           (file+datetree+prompt "~/org/organize.org" "Meetings")
           "** %^{Meeting}\n%T\n%?\n")
          ("dt" "Done - Task" entry
           (file+headline "~/org/organize.org" "Tasks")
           "* DONE %^{Task}\nSCHEDULED: %^t\n%?")
          ("q" "Quick note" item
           (file+headline "~/org/organize.org" "Quick notes"))
          ("b" "Book" entry
           (file+datetree "~/org/reading.org" "Books")
           "* %^{Title}  %^g
  %i
  ,*Author(s):* %^{Author}
  ,*Rating:* %^{Rating|1|2|3|4|5|6|7|8|9|10}

  %?

  ,*Finished on:* %^t")
          ("p" "Paper" entry
           (file+datetree "~/org/reading.org" "Papers")
           "* %^{Title}  %^g
  %i
  ,*Author(s):* %^{Author}

  %?

  ,*Finished on:* %^t")))

#+END_SRC

From [[http://pages.sachachua.com/.emacs.d/Sacha.html#sec-1-8][Sacha]], "~org-refile~ lets you organize notes by typing in the headline to
file them under."

#+name: org
#+BEGIN_SRC emacs-lisp
  (setq org-reverse-note-order t)
  (setq org-refile-use-outline-path 'file)
  (setq org-refile-allow-creating-parent-nodes 'confirm)
  (setq org-refile-use-cache nil)
  (setq org-log-refile t)
  (setq org-refile-targets '((org-agenda-files . (:maxlevel . 6))))
  (setq org-blank-before-new-entry nil)
#+END_SRC

*** Agenda

#+name: org
#+BEGIN_SRC emacs-lisp
  (use-package org-agenda
    :init
    (progn
      (bind-key "i" 'org-agenda-clock-in org-agenda-mode-map)
      (bind-key "C-c C-u" 'org-agenda-undo org-agenda-mode-map)
      (dolist (dir '("left" "right" "up" "down"))
        (bind-key (kbd (format "S-<%s>" dir)) nil org-agenda-mode-map))
      (bind-key "<C-S-down>" 'org-agenda-priority-down org-agenda-mode-map)
      (bind-key "<C-S-up>" 'org-agenda-priority-up org-agenda-mode-map)
      (bind-key "<C-S-left>" 'org-agenda-do-date-earlier org-agenda-mode-map)
      (bind-key "<C-S-right>" 'org-agenda-do-date-later org-agenda-mode-map)))

  (setq org-agenda-files
        (delq nil
              (mapcar (lambda (x) (and (file-exists-p x) x))
                      '("~/org/organize.org"))))

  (setq org-refile-targets '(("organize.org" :maxlevel . 1)
                             ("someday.org" :level . 2)))

  (setq org-agenda-span 3)
  (setq org-agenda-sticky nil)
  (setq org-agenda-show-log t)
  (setq org-agenda-skip-scheduled-if-done t)
  (setq org-agenda-skip-deadline-if-done t)
  (setq org-agenda-skip-deadline-prewarning-if-scheduled 'pre-scheduled)
  (setq org-agenda-time-grid
        '((daily today require-timed)
         "----------------"
         (800 1000 1200 1400 1600 1800)))
  (setq org-columns-default-format "%50ITEM %12SCHEDULED %TODO %3PRIORITY %Effort{:} %TAGS")
  (setq org-agenda-start-on-weekday 6)
#+END_SRC

Org Habits config:

#+name: org
#+BEGIN_SRC emacs-lisp
  (setq org-habit-graph-column 80)
  (setq org-habit-show-habits-only-for-today t)
#+END_SRC

We want to archive things under their CLOSED or archived date.

#+name: org
#+BEGIN_SRC emacs-lisp
(setq org-archive-location "%s_archive::datetree/")
#+END_SRC

*** Projects and Todos

#+name: org
#+BEGIN_SRC emacs-lisp
  (setq org-todo-keywords
   '((sequence
      "TODO(t)"
      "WAITING(w@/!)"
      "SOMEDAY(.)"
      "|" "DONE(x!)" "CANCELLED(c@)")))

  (setq org-todo-keyword-faces
        '(("TODO" . (:foreground "green" :weight bold))
          ("DONE" . (:foreground "cyan" :weight bold))
          ("WAITING" . (:foreground "red" :weight bold))
          ("SOMEDAY" . (:foreground "gray" :weight bold))
          ("CANCELLED" . (:foreground "gray" :weight bold))))

  (setq org-log-done 'time)
  (setq org-log-done 'note)
  (setq org-log-into-drawer t)

  (setq org-tags-exclude-from-inheritance '("project"))

  (setq org-tag-alist '(("@work" . ?b)
                        ("@home" . ?h)
                        ("@writing" . ?w)
                        ("@errands" . ?e)
                        ("@hacking" . ?h)
                        ("@phone" . ?p)
                        ("@computer" . ?c)
                        ("@studying" . ?s)
                        ("@reading" . ?r)
                        ("lowenergy" . ?0)
                        ("highenergy" . ?1)))

  (add-to-list 'org-global-properties
    '("Effort_ALL". "0:05 0:15 0:30 1:00 2:00 3:00 4:00"))
#+END_SRC

*** Misc

Linked images are shown by default.

#+name: org
#+BEGIN_SRC emacs-lisp
    (setq org-startup-with-inline-images t)
#+END_SRC

Show special characters using UTF8.

#+name: org
#+BEGIN_SRC emacs-lisp
  (setq org-pretty-entities t)
#+END_SRC

We need to unbind ~S-<left|right|up|down>~ because ~org-mode~ steals these from
~windmove~, which is not cool.

#+name: modes
#+BEGIN_SRC emacs-lisp
  (dolist (dir '("left" "right" "up" "down"))
    (define-key org-mode-map (kbd (format "S-<%s>" dir)) nil))
#+END_SRC
** Mail (mu4e)
   #+name: mail
   #+BEGIN_SRC emacs-lisp
     (add-to-list 'load-path "/usr/local/Cellar/mu/HEAD/share/emacs/site-lisp/mu4e/")

     (require 'mu4e)
     (setq mail-user-agent 'mu4e-user-agent)

     (setq mu4e-get-mail-command "offlineimap"
           mu4e-update-interval 300)

     (global-set-key (kbd "C-c m") 'mu4e)

     (setq mu4e-attachment-dir "~/Downloads")

     (setq mu4e-sent-messages-behavior 'delete)

     (setq mu4e-view-show-images t)
     (when (fboundp 'imagemagick-register-types)
       (imagemagick-register-types))

     (setq message-signature ""
           mu4e-compose-signature-auto-include nil)

   #+END_SRC

   #+name: mail
   #+BEGIN_SRC emacs-lisp
     (setq mu4e-drafts-folder "/[Gmail].Drafts")
     (setq mu4e-sent-folder   "/[Gmail].Sent Mail")
     (setq mu4e-trash-folder  "/[Gmail].Trash")

     (setq mu4e-maildir-shortcuts
           '( ("/INBOX"   . ?i)
              ("/[Gmail].Sent"    . ?s)
              ("/[Gmail].Trash"   . ?t)
              ("/[Gmail].All Mail" . ?a)))

     (setq message-send-mail-function 'smtpmail-send-it
           smtpmail-stream-type 'starttls
           smtpmail-default-smtp-server "smtp.gmail.com"
           smtpmail-smtp-server "smtp.gmail.com"
           smtpmail-smtp-service 587)

     (setq user-mail-address "isaachodes@gmail.com"
           user-full-name  "Isaac Hodes")

     ;; don't kqeep message buffers around
     (setq message-kill-buffer-on-exit t)
   #+END_SRC
** Python settings

This lets us use the iPython kernel as the inferior Python process.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (setq python-shell-interpreter "ipython"
          python-shell-prompt-regexp "In \\[[0-9]+\\]: "
          python-shell-prompt-output-regexp "Out \\[[0-9]+\\]: "
          python-shell-completion-setup-code
          "from IPython.core.completerlib import module_completion"
          python-shell-completion-module-string-code
          "';'.join(module_completion('''%s'''))\n"
          python-shell-completion-string-code
          "';'.join(get_ipython().Completer.all_completions('''%s'''))\n")
  #+END_SRC

Some simple EIN customizations.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (setq ein:use-auto-complete 1)
    (setq ein:console-args '("--gui=osx" "--matplotlib=osx" "--colors=Linux"))

    (defun ein:load-notebooks ()
      (interactive)
      (ein:notebooklist-load)
      (ein:notebooklist-open))
  #+END_SRC
** Clojure settings

Clojure-mode is useful for ~.edn~, ~.cljs~, and ~.cljx~ files as well.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (nconc auto-mode-alist '(("\\.edn\\'" . clojure-mode)
                             ("\\.cljs\\'" . clojure-mode)
                             ("\\.cljx\\'" . clojure-mode)))
  #+END_SRC

We make the [[https://github.com/clojure-emacs/cider][Cider]] (Clojure IDE) experience a bit better.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (require 'clojure-mode)
    (add-hook 'cider-mode-hook 'cider-turn-on-eldoc-mode)
    (add-hook 'cider-repl-mode-hook 'paredit-mode)
    (setq nrepl-hide-special-buffers t)
    (setq cider-auto-select-error-buffer t)
  #+END_SRC

Some common Clojure functions look better with different indentation, so we set
those here.

  #+name: modes
  #+BEGIN_SRC emacs-lisp
    (define-clojure-indent
      (defroutes 'defun)
      (GET 2)
      (POST 2)
      (PUT 2)
      (DELETE 2)
      (HEAD 2)
      (ANY 2)
      (context 2)
      (form-to 1)
      (match 1)
      (are 2)
      (select 1)
      (insert 1)
      (update 1)
      (delete 1)
      (run* 1)
      (fresh 1)
      (extend-freeze 2)
      (extend-thaw 1))
  #+END_SRC
** Scala settings

   #+name: modes
   #+BEGIN_SRC emacs-lisp
     (use-package ensime
       :init
       (progn
         (add-hook 'scala-mode-hook 'ensime-scala-mode-hook)
         (setq ensime-sem-high-faces
               '((var . (:foreground "#ff2222"))
                 (val . (:foreground "#dddddd"))
                 (varField . (:foreground "#ff3333"))
                 (valField . (:foreground "#dddddd"))
                 (functionCall . (:foreground "#84BEE3"))
                 (param . (:foreground "#ffffff"))
                 (class . font-lock-type-face)
                 (trait . (:foreground "#084EA8"))
                 (object . (:foreground "#026DF7"))
                 (package . font-lock-preprocessor-face)))))
   #+END_SRC
** Coq
   #+name: modes
   #+BEGIN_SRC emacs-lisp
     (load-file "/usr/local/share/emacs/site-lisp/ProofGeneral/generic/proof-site.el")
     (autoload 'coq-mode "coq" "Major mode for editing Coq vernacular." t)
     (setq auto-mode-alist (cons '("\\.v$" . coq-mode) auto-mode-alist))
     (eval-after-load 'coq-mode '(define-key coq-mode-map (kbd "C-c C-.") 'proof-goto-point))
   #+END_SRC
* Gittit
  ~gittit~ is a little library I wrote to connect local files to GitHub repos.

  These are our utility functions.

  #+name: gittit
  #+BEGIN_SRC emacs-lisp
    (defun gittit:base-github-url ()
      (let* ((git-url (shell-command-to-string "git config --get remote.origin.url"))
             (http-url (replace-regexp-in-string "git@" "" git-url))
             (http-url (replace-regexp-in-string "\.git" "" http-url))
             (http-url (replace-regexp-in-string ":" "/" http-url))
             (http-url (replace-regexp-in-string "\n" "" http-url)))
        http-url))

    (defun gittit:current-branch-name ()
      (replace-regexp-in-string "\n" "" (shell-command-to-string "git rev-parse --abbrev-ref HEAD")))

    (defun gittit:parent-directory (dir)
      (unless (equal "/" dir)
        (file-name-directory (directory-file-name dir))))

    (defun gittit:base-git-directory (filename)
      (let ((base-dir (file-name-directory filename)))
        (if (file-exists-p (concat base-dir ".git"))
          base-dir
          (gittit:base-git-directory (gittit:parent-directory base-dir)))))

    (defun gittit:github-url-for-file (filename)
      (format "http://%s/blob/%s/%s"
              (gittit:base-github-url)
              (gittit:current-branch-name)
              (replace-regexp-in-string (gittit:base-git-directory filename) "" filename)))

    (defun gittit:github-url-for-line (filename start &optional end)
      (format (concat (gittit:github-url-for-file filename) (if end "#L%s-L%s" "#L%s"))
              start
              end))
  #+END_SRC

  These are the public exports:

  #+name: gittit
  #+BEGIN_SRC emacs-lisp
    (defun github-url-for-line  (filename start &optional end)
      "Returns, echoes, and kills the GitHub URL for FILENAME between START and optionally END."
      (interactive (cons (buffer-file-name)
                         (if (use-region-p)
                            (list (region-beginning) (region-end))
                            (list (point)))))
      (let* ((url (gittit:github-url-for-file filename))
             (start-line (1+ (count-lines 1 start)))
             (url (if end
                      (format "%s#L%s-L%s" url start-line (count-lines 1 end))
                      (format "%s#L%s" url start-line))))
        (kill-new url)
        (message url)
        url))

    (defun browse-github-url-for-line (filename start &optional end)
      "Navigate to the GitHub URL for FILENAME between START and optionally END."
      (interactive (cons (buffer-file-name)
                         (if (use-region-p)
                            (list (region-beginning) (region-end))
                            (list (point)))))
      (browse-url (if end (github-url-for-line filename start end)
                    (github-url-for-line filename start))))
  #+END_SRC

  Under the [[http://www.apache.org/licenses/LICENSE-2.0.html][Apache 2.0 License]].
* Misc. Functions
  A bunch of little utility functions created here and elsewhere.
  #+name: functions
  #+BEGIN_SRC emacs-lisp
    (defun clear-shell-buffer ()
      "Clear the current buffer"
      (interactive)
      (let ((comint-buffer-maximum-size 0))
         (comint-truncate-buffer)))

    (defun osx:copy-region (start end)
      "Copy the region to OSX's clipboard."
      (interactive (list (region-beginning) (region-end)))
      (shell-command-on-region start end "pbcopy")
      (message "Copied to OSX clipboard!"))

    (defun osx:paste ()
      "Copy the region to OSX's clipboard."
      (interactive)
      (insert (shell-command-to-string "pbpaste"))
      (message "Pasted from OSX clipboard!"))

    (defun osx:copy-kill ()
      "Copy the current kill text to OSX's clipboard."
      (interactive)
      (with-temp-buffer
        (yank)
        (shell-command-on-region 1 (point-max) "pbcopy")))

    (defun set-exec-path-from-shell-PATH ()
      (let ((path-from-shell (replace-regexp-in-string
                              "[ \t\n]*$"
                              ""
                              (shell-command-to-string "$SHELL --login -i -c 'echo $PATH'"))))
        (setenv "PATH" path-from-shell)
        (setq eshell-path-env path-from-shell) ; for eshell users
        (setq exec-path (split-string path-from-shell path-separator))))

    ;;http://emacsredux.com/blog/2013/05/22/smarter-navigation-to-the-beginning-of-a-line/
    (defun smarter-move-beginning-of-line (arg)
      "Move point back to indentation of beginning of line.

    Move point to the first non-whitespace character on this line.
    If point is already there, move to the beginning of the line.
    Effectively toggle between the first non-whitespace character and
    the beginning of the line.

    If ARG is not nil or 1, move forward ARG - 1 lines first.  If
    point reaches the beginning or end of the buffer, stop there."
      (interactive "^p")
      (setq arg (or arg 1))

      ;; Move lines first
      (when (/= arg 1)
        (let ((line-move-visual nil))
          (forward-line (1- arg))))

      (let ((orig-point (point)))
        (back-to-indentation)
        (when (= orig-point (point))
          (move-beginning-of-line 1))))

    (defun endless/load-gh-pulls-mode ()
      "Start `magit-gh-pulls-mode' only after a manual request."
      (interactive)
      (require 'magit-gh-pulls)
      (add-hook 'magit-mode-hook 'turn-on-magit-gh-pulls)
      (magit-gh-pulls-mode 1)
      (magit-gh-pulls-reload))

  #+END_SRC
* Bindings

  Global and some mode-specific bindings that need to be cleaned up.

  #+name: bindings
  #+BEGIN_SRC emacs-lisp
    (global-set-key (kbd "C-x C-b") 'ibuffer)
    (global-set-key (kbd "C-s") #'isearch-forward-regexp)
    (global-set-key (kbd "C-r") #'isearch-backward-regexp)

    (global-set-key (kbd "<f1>") #'eshell)
    (global-set-key (kbd "<f3>") #'occur)
    (global-set-key (kbd "<f4>") #'ido-recentf-open)
    (global-set-key (kbd "<f5>") #'highlight-symbol-at-point)
    (global-set-key (kbd "<f6>") #'revert-this-buffer)

    (global-set-key (kbd "C-c M-w") #'whitespace-mode)

    (global-set-key (kbd "M-j") '(lambda () (interactive) (join-line -1)))

    (global-set-key (kbd "C-x t") '(lambda () (interactive) (insert "TODO(ihodes): ")))

    (global-set-key (kbd "M-s-â‰¥") #'mc/mark-next-lines)

    (global-set-key (kbd "C-x w") #'delete-trailing-whitespace)

    (global-set-key (kbd "C-x C-d") #'ido-dired)

    (global-set-key (kbd "C-c C-e") #'eval-buffer)

    (define-key 'help-command "A" #'apropos) ;; (C-h a)

    (eval-after-load #'comint-mode-hook
      '(progn
         (define-key comint-mode-map (kbd "C-c C-t") 'comint-truncate-buffer)))

    ;; remap C-a to `smarter-move-beginning-of-line'
    (global-set-key [remap move-beginning-of-line]
                    'smarter-move-beginning-of-line)

    (eval-after-load 'js
      '(progn
         (define-key js-mode-map (kbd "C-x ;")
           (lambda ()
             (interactive)
             (insert "console.log();")
             (backward-char 2)))))

    (defun revert-this-buffer ()
      (interactive)
      (revert-buffer nil t t)
      (message (concat "Reverted buffer " (buffer-name))))

    (defun ido-recentf-open ()
      "Use `ido-completing-read' to \\[find-file] a recent file"
      (interactive)
      (if (find-file (ido-completing-read "Find recent file: " recentf-list))
          (message "Opening file...")
        (message "Aborting")))
  #+END_SRC
* Projects

This is a simple & hacky way to start asynchronous processes associated with
projects I frequently work on.

** TODO Manage a list of processes per project, and commands for restarting them etc. Upstart?
** CycleDash
  Found on [[https://github.com/hammerlab/cycledash][GitHub]].
  #+name: projects
  #+BEGIN_SRC emacs-lisp
    (defun cycledash:start-server ()
      "Start the CycleDash server."
      (interactive)
      (async-shell-command "cd ~/workspace/cycledash/ && source venv/bin/activate && ./run.sh"
                           "*CycleDash:./run.sh*"))

    (defun cycledash:start-worker ()
      "Start a CycleDash worker named WORK."
      (interactive)
      (async-shell-command "cd ~/workspace/cycledash/ && source venv/bin/activate && ./worker.sh WORK"
                           "*CycleDash:./worker.sh*"))

    (defun cycledash:start-gulp ()
      "Start the gulp dev js builder."
      (interactive)
      (async-shell-command "cd ~/workspace/cycledash/ && gulp"
                           "*CycleDash: gulp*"))

    (defun cycledash:start ()
      "Start all cycledash services"
      (interactive)
      (dolist (start '(cycledash:start-server cycledash:start-worker cycledash:start-gulp))
        (funcall start)))
  #+END_SRC
** VCF.js
  Found on [[https://github.com/ihodes/vcf.js][GitHub]].
  #+name: projects
  #+BEGIN_SRC emacs-lisp
    (defun vcf-js:test ()
      "Run the vcf test suite"
      (interactive)
      (async-shell-command "cd ~/workspace/vcf.js/ && mocha test/test.js"
                           "*vcf.js: tests*"))

    (defun vcf-js:server ()
      "Run the vcf test server"
      (interactive)
      (async-shell-command "cd ~/workspace/vcf.js/ && http-server"
                           "*vcf.js: server*"))
  #+END_SRC
** Idiogrammatik.js

   Found on [[https://github.com/hammerlab/idiogrammatik][GitHub]].

   #+name: projects
   #+BEGIN_SRC emacs-lisp
    (defun idiogrammatik:server ()
      "Run the idiogrammatik server"
      (interactive)
      (async-shell-command "cd ~/workspace/idiogrammatik/ && http-server -p 8989"
                           "*idiogrammatik: server*"))
   #+END_SRC
* Configuration file layout

  Here I define the ~emacs.el~ file generated by the code in this org file.

  The below block describes how the code above should be organized within the
  generated ~emacs.el~.

  #+BEGIN_SRC emacs-lisp :tangle yes :noweb no-export :exports code
    ;;;; This file generated from `emacs.org` in this directory.

    <<init>>
    <<look-and-feel>>
    <<packages>>
    <<modes>>
    <<defaults>>
    <<functions>>
    <<gittit>>
    <<bindings>>
    <<projects>>
  #+END_SRC

* Archived
